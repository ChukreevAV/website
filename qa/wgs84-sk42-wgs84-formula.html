<!--#include virtual="/inc/header02.txt" -->
<title>GIS-Lab: Формулы пересчета координат из WGS-84 в СК-42 и обратно</title>
<!--#include virtual="/inc/header2.txt" -->
<div class="cont">
<div class="col1">

<ul class="path">
   <li class="first"><a href="/">Главная</a></li>
   <li><a href="/qa.html">Статьи</a></li>
</ul>
<!--Contents start-->
<h1>Формулы пересчета координат из WGS-84 в СК-42 и обратно</h1>
<p class="ann">Для тех, кто хочет разобраться в математике пересчета и\или встроить конвертацию в свою программу.</p>
<p class="discuss discuss_top"><span><!--#include virtual="/scripts/forum-comments-num.php?i=211"--></span></p>

            <p>В этой статье приводятся функции для преобразования геодезических координат из координатной системы Пулково 1942 в координатную систему WGS 1984 и обратно.</p>
            <p>В данном примере используются три параметра трансформирования (dx, dy, dz), остальные параметры равны 0. Если вы хотите использовать 7 параметров (дополнительно wx, wy, wz, ms), измените значения угловых элементов трансформирования. Линейные элементы трансформирования также могут быть другими, в данной примере используются  линейные элементы трансформирования из <a href="/docs/legislation.html">ГОСТ 51794-2001</a> (<a href="/qa/datum-transform-sets.html">другие</a> возможные наборы элементов) . Для рассчета используется формулы Бурса-Вольфа (<a href="/qa/datum-transform-methods.html">подробнее</a>).</p>
            <p> Все угловые значения передаются и возвращаются в десятичных градусах (dd.ddddd), высоты передаются и возвращаются в метрах.</p>
            <p>Результаты пересчета <a href="/qa/coord-transforms.html">проверены с помощью Proj</a> и совпадают с его результатами до 7-го знака после запятой. </p>
            <pre>Const Pi As Double = 3.14159265358979 ' Число Пи
Const ro As Double = 206264.8062 ' Число угловых секунд в радиане

' Эллипсоид Красовского
Const aP As Double = 6378245 ' Большая полуось
Const alP As Double = 1 / 298.3 ' Сжатие
Const e2P As Double = 2 * alP - alP ^ 2 ' Квадрат эксцентриситета

' Эллипсоид WGS84 (GRS80, эти два эллипсоида сходны по большинству параметров)
Const aW As Double = 6378137 ' Большая полуось
Const alW As Double = 1 / 298.257223563 ' Сжатие
Const e2W As Double = 2 * alW - alW ^ 2 ' Квадрат эксцентриситета

' Вспомогательные значения для преобразования эллипсоидов
Const a As Double = (aP + aW) / 2
Const e2 As Double = (e2P + e2W) / 2
Const da As Double = aW - aP
Const de2 As Double = e2W - e2P

' Линейные элементы трансформирования, в метрах
Const dx As Double = 23.92
Const dy As Double = -141.27
Const dz As Double = -80.9

' Угловые элементы трансформирования, в секундах
Const wx As Double = 0
Const wy As Double = 0
Const wz As Double = 0

' Дифференциальное различие масштабов
Const ms As Double = 0

Function WGS84_SK42_Lat(Bd, Ld, H) As Double
    WGS84_SK42_Lat = Bd - dB(Bd, Ld, H) / 3600
End Function

Function SK42_WGS84_Lat(Bd, Ld, H) As Double
    SK42_WGS84_Lat = Bd + dB(Bd, Ld, H) / 3600
End Function

Function WGS84_SK42_Long(Bd, Ld, H) As Double
    WGS84_SK42_Long = Ld - dL(Bd, Ld, H) / 3600
End Function

Function SK42_WGS84_Long(Bd, Ld, H) As Double
    SK42_WGS84_Long = Ld + dL(Bd, Ld, H) / 3600
End Function

Function dB(Bd, Ld, H) As Double
    Dim B, L, M, N As Double
    B = Bd * Pi / 180
    L = Ld * Pi / 180
    M = a * (1 - e2) / (1 - e2 * Sin(B) ^ 2) ^ 1.5
    N = a * (1 - e2 * Sin(B) ^ 2) ^ -0.5
    dB = ro / (M + H) * (N / a * e2 * Sin(B) * Cos(B) * da _ + (N ^ 2 / a ^ 2 + 1) * N * Sin(B) * Cos(B) * de2 / 2 _ - (dx * Cos(L) + dy * Sin(L)) * Sin(B) + dz * Cos(B)) _ - wx * Sin(L) * (1 + e2 * Cos(2 * B)) _ + wy * Cos(L) * (1 + e2 * Cos(2 * B)) _ - ro * ms * e2 * Sin(B) * Cos(B)
End Function

Function dL(Bd, Ld, H) As Double
    Dim B, L, N As Double
    B = Bd * Pi / 180
    L = Ld * Pi / 180
    N = a * (1 - e2 * Sin(B) ^ 2) ^ -0.5
    dL = ro / ((N + H) * Cos(B)) * (-dx * Sin(L) + dy * Cos(L)) _ + Tan(B) * (1 - e2) * (wx * Cos(L) + wy * Sin(L)) - wz
End Function

Function WGS84Alt(Bd, Ld, H) As Double
    Dim B, L, N, dH As Double
    B = Bd * Pi / 180
    L = Ld * Pi / 180
    N = a * (1 - e2 * Sin(B) ^ 2) ^ -0.5
    dH = -a / N * da + N * Sin(B) ^ 2 * de2 / 2 _ + (dx * Cos(L) + dy * Sin(L)) * Cos(B) + dz * Sin(B) _ - N * e2 * Sin(B) * Cos(B) * (wx / ro * Sin(L) - wy / ro * Cos(L)) _ + (a ^ 2 / N + H) * ms
    WGS84Alt = H + dH
End Function</pre>
            <p>Код пересчетов базируется на примере Olexa Riznyk, <a href="http://www.olexa.com.ua" target="_blank" class="external">www.olexa.com.ua</a></p>
            <h2>Модуль для Excel </h2>
            <h3>Установка и настройка </h3>
            <p>Приведенный ниже код нужно скопировать и открыть окно редактора Visual Basic программы Microsoft Excel (Сервис\Макрос\Редактор Visual Basic). Далее, необходимо создать новый модуль, для этого  нужно выбрать в меню Insert\Module и вставить в появившийся модуль скопированный код.</p>
            <p>Так же можно <a href="/other/wgs84-sk42-wgs84.bas">загрузить этот код в виде готового модуля</a> и импортировать его (File\Import File...) в окне редактора Visual Basic.</p>
            <h3>Работа</h3>
            <p>После установки, в рабочей области Excel станут доступны следующие функции:</p>
            <p>Пересчет широты из WGS-84 в СК-42: WGS84_SK42_Lat(Lat,Long,Height) <br />
              Пересчет долготы из WGS-84 в СК-42: WGS84_SK42_Long(Lat,Long,Height)<br />
              Пересчет широты из СК-42 в WGS-84: SK42_WGS84_Lat(Lat,Long,Height)<br />
              Пересчет долготы из СК-42 в WGS-84: SK42_WGS84_Long(Lat,Long,Height) </p>
            <p>Для выполнения пересчета, нужно использовать вышепреведенные функции подставляя им в качестве аргументов значения широты и долготы в десятичных градусах и высоты в метрах, например, в ячейку Excel можно ввести: </p>
            <p>=<strong>WGS84_SK42_Lat</strong>(50;50;0) и получить результат <strong>49.99980414</strong></p>
            <p>Если ввод формулы приводит к ошибке, убедитесь, что в документе разрешены Макросы (Сервис\Макрос\Безопасность - Средняя)</p>
            <p>Для вычислений также можно использовать готовую таблицу в формате MS Excel (<a href="/other/wgs84-sk42-wgs84.xls">скачать</a>).</p>
            <p class="discuss">
				<span><!--#include virtual="/scripts/forum-comments-num.php?i=211"--></span>
			</p>
			<div class="links">
			   <h2>Ссылки по теме</h2>
			   <ul>
			               <li><a href="/qa/datum-transform-sets.html">Переход от одной системы координат к другой - наборы параметров</a></li>
			               <li><a href="/qa/gps-dnrgarmin.html">Загрузка данных с GPS в нужной системе координат (DNRGarmin)</a></li>
			               <li><a href="/qa/gps.html">Настройка GPS для работы с российскими картами</a></li>
	                  </ul>
			</div>
<!--Contents end-->
<!--#include virtual="/scripts/date.php" -->
<p class="status"><span>Дата создания: 07.12.2004
<br>Автор(ы): <a href="/forum/memberlist.php?mode=viewprofile&u=2" target="_blank">Максим Дубинин</a></span></p>
<!--#include virtual="/inc/footer2.php" -->
